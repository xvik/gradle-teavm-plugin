plugins {
    id 'com.gradle.plugin-publish' version '1.1.0'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id 'signing'
    id 'net.researchgate.release' version '3.0.2'
    id 'ru.vyarus.quality' version '4.9.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
    id 'ru.vyarus.java-lib' version '2.4.0'
    id 'ru.vyarus.github-info' version '1.5.0'
    id 'com.github.ben-manes.versions' version '0.46.0'
    id "pl.droidsonroids.jacoco.testkit" version "1.0.9"
}

// teavm compiled for 11
sourceCompatibility = "11"

wrapper {
    gradleVersion = '7.6'
}

repositories { mavenLocal(); mavenCentral(); gradlePluginPortal() }
dependencies {
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.7.3'

    // compiled with this version, and this version used at runtime to support enums, but compiler could be executed
    // with different version
    implementation 'org.teavm:teavm-core:0.8.0'
    implementation 'org.teavm:teavm-classlib:0.8.0'
    implementation 'org.teavm:teavm-tooling:0.8.1'

    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
}

group = 'ru.vyarus'
description = 'Gradle TeaVM plugin'

github {
    user 'xvik'
    license 'MIT'
}

pom {
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email 'vyarus@gmail.com'
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = findProperty('sonatypeUser')
            password = findProperty('sonatypePassword')
        }
    }
}

// skip signing for jitpack (snapshots)
tasks.withType(Sign) {onlyIf { !System.getenv('JITPACK') }}

// Required signing properties for release: signing.keyId, signing.password and signing.secretKeyRingFile
// (https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials)

javaLib {
    // don't publish gradle metadata artifact
    withoutGradleMetadata()
}


gradlePlugin {
    plugins {
        teavmPlugin {
            id = 'ru.vyarus.teavm'
            displayName = project.description
            description = 'Gradle TeaVM plugin'
            tags.set(['teavm', 'java', 'javascript', 'webassembly'])
            implementationClass = 'ru.vyarus.gradle.plugin.teavm.TeavmPlugin'
        }
    }
}

release.git.requireBranch.set('master')

afterReleaseBuild {
    dependsOn = [
            'publishMavenPublicationToSonatypeRepository',
            'closeAndReleaseSonatypeStagingRepository',
            publishPlugins]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat 'full'
    }
    maxHeapSize = '512m'
    doLast {
        sleep(1000)
    }
}

dependencyUpdates.revision = 'release'
