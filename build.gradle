plugins {
    id 'com.gradle.plugin-publish' version '1.3.1'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id 'signing'
    id 'net.researchgate.release' version '3.1.0'
    id 'ru.vyarus.quality' version '6.0.0'
    id 'com.github.spotbugs' version '5.2.5'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'ru.vyarus.java-lib' version '3.0.0'
    id 'ru.vyarus.github-info' version '2.0.0'
    id 'com.github.ben-manes.versions' version '0.52.0'
    id "pl.droidsonroids.jacoco.testkit" version "1.0.12"
}

java {
    // teavm compiled for 11
    sourceCompatibility = "11"
}

wrapper {
    gradleVersion = '8.14.3'
    distributionType = Wrapper.DistributionType.BIN
}

ext {
    teavm = '0.12.3'
}

repositories { mavenLocal(); mavenCentral(); gradlePluginPortal(); maven { url "https://teavm.org/maven/repository" } }
dependencies {
    // compiled with this version, and this version used at runtime to support enums, but compiler could be executed
    // with different version
    implementation "org.teavm:teavm-core:$teavm"
    implementation "org.teavm:teavm-classlib:$teavm"
    implementation "org.teavm:teavm-tooling:$teavm"

    testImplementation 'org.spockframework:spock-core:2.4-M6-groovy-3.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

group = 'ru.vyarus'
description = 'Gradle TeaVM plugin'

github {
    user = 'xvik'
    license = 'MIT'
}

maven.pom {
    developers {
        developer {
            id = 'xvik'
            name = 'Vyacheslav Rusakov'
            email = 'vyarus@gmail.com'
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri("https://ossrh-staging-api.central.sonatype.com/service/local/")
            snapshotRepositoryUrl = uri("https://central.sonatype.com/repository/maven-snapshots/")
            username = findProperty('sonatypeUser')
            password = findProperty('sonatypePassword')
        }
    }
}

// skip signing for jitpack (snapshots)
tasks.withType(Sign).configureEach { onlyIf { !System.getenv('JITPACK') } }

// Required signing properties for release: signing.keyId, signing.password and signing.secretKeyRingFile
// (https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials)

javaLib {
    // don't publish gradle metadata artifact
    withoutGradleMetadata()
}


gradlePlugin {
    plugins {
        teavmPlugin {
            id = 'ru.vyarus.teavm'
            displayName = project.description
            description = 'Gradle TeaVM plugin'
            tags.set(['teavm', 'java', 'javascript', 'webassembly'])
            implementationClass = 'ru.vyarus.gradle.plugin.teavm.TeavmPlugin'
        }
    }
}

release.git.requireBranch.set('master')

afterReleaseBuild {
    dependsOn = [
            'publishMavenPublicationToSonatypeRepository',
            'closeAndReleaseSonatypeStagingRepository',
            publishPlugins]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat = 'full'
    }
    maxHeapSize = '2g'
    doLast {
        sleep(1000)
    }
}

dependencyUpdates.revision = 'release'
