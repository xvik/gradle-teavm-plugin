plugins {
    id 'java'
    id 'ru.vyarus.teavm'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.teavm:teavm-classlib:$rootProject.teavmVersion"
    implementation "org.teavm:teavm-jso-apis:$rootProject.teavmVersion"

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.teavm:teavm-junit:$rootProject.teavmVersion"
}

teavm {
    version = getProperty('teavm')
    dev = false
    debug = true
    mainClass = 'example.Client'
}

// https://teavm.org/docs/tooling/testing.html
// NOTE: compilation options above are not used for test!
test {
    systemProperty 'teavm.junit.target', file('build/teavm-test').absolutePath
    systemProperty 'teavm.junit.threads', '1'
    systemProperty 'teavm.junit.js', 'true'
    systemProperty 'teavm.junit.js.runner', 'browser-chrome'
    systemProperty 'teavm.junit.js.decodeStack', 'true'
}

tasks.register('buildWebapp', Copy) {
    from('src/main/webapp')
    from(compileTeavm) {
        into 'js'
    }
    into 'build/webapp'
}